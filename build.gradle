plugins {
    id 'io.papermc.paperweight.userdev' version '1.5.11'
    id 'java-library'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.github.Outspending'
    version = '0.0.1'

    sourceCompatibility = 17
    targetCompatibility = 17

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

}

subprojects {

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
    }

    // publish NMS apis
    // this is required because consumers require getting the POM
    // files from this project's dependencies, even if they're completely
    // shaded and included in this file.
    //
    // not optimal because this leads to more network calls when downloading
    // the api for the first time.
    publishing {
        publications {
            create('maven', MavenPublication) {
                groupId = 'com.github.Outspending'
                artifactId = project.name

                from components.java
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}



// NMS Versions
def biomesNMSVersions = ["1.19_R2", "1.19_R3", "1.20_R1", "1.20_R2", "1.20_R3"]

dependencies {
    paperweight.paperDevBundle("1.20-R0.1-SNAPSHOT")

    implementation project(':NMS:Wrapper')
    biomesNMSVersions.each { version ->
        implementation project(":NMS:${version}")
    }

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

}

// The file for publication.
def buildFile = layout.buildDirectory.file("libs/BiomesAPI-${project.version}.jar")

publishing {
    publications {
        create('maven', MavenPublication) {
            groupId = 'com.github.Outspending'
            artifactId = 'BiomesAPI'

            artifact buildFile
            from components.java
        }
    }
}

publishMavenPublicationToMavenLocal {
    // Throws an error if we don't explicitly say we're using the output
    // of reobfJar in the final build.
    dependsOn(reobfJar)
}

reobfJar {
    // Make sure it's the correct name for when it's inserted into the publication
    outputJar.set(buildFile)
}

wrapper {
    gradleVersion = '8.1.1'
    distributionType = Wrapper.DistributionType.ALL
}