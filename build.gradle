plugins {
    id 'java'
    id 'maven-publish'

    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.papermc.paperweight.userdev' version '1.5.11'
}

allprojects {

    group = 'com.github.Outspending'
    version = '0.0.1'

    sourceCompatibility = 17
    targetCompatibility = 17

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

}

// The file for publication.
def buildFile = layout.buildDirectory.file("libs/BiomesAPI-${project.version}.jar")

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
    }

    publishing {
        publications {
            create('maven', MavenPublication) {
                groupId = 'com.github.Outspending'

                artifact buildFile

                artifactId = 'BiomesAPI'
                from components.java
            }
        }
    }

    publishMavenPublicationToMavenLocal {
        // Throws an error if we don't explicitly say we're using the output
        // of reobfJar in the final build.
        dependsOn(reobfJar)
    }

    reobfJar {
        // Make sure it's the correct name for when it's inserted into the publication
        outputJar.set(buildFile)
    }

    wrapper {
        gradleVersion = '8.1.1'
        distributionType = Wrapper.DistributionType.ALL
    }

}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

// NMS Versions
def biomesNMSVersions = ["1.19_R2", "1.19_R3", "1.20_R1", "1.20_R2", "1.20_R3"]

dependencies {
    paperweight.paperDevBundle("1.20-R0.1-SNAPSHOT")

    implementation project(':BiomesAPI-NMS')
    biomesNMSVersions.each { version ->
        implementation project(path: ":BiomesAPI-${version}", configuration: 'reobf')
    }

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

}
